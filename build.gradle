import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.1.9.RELEASE'
        repos = {
            mavenLocal()
            mavenCentral()
            maven { url 'http://repo.maven.apache.org/maven2' }
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url 'https://plugins.gradle.org/m2/' }
        }
    }

    repositories repos

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:1.3.1")
    }
}

repositories repos

group = 'com.mayreh.typo'
version = '0.1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation('org.springframework.boot:spring-boot-starter-web')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }

    dependencies {
        dependency 'org.projectlombok:lombok:1.18.10'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '--release' << '11'
}

node {
    nodeModulesDir = file("${projectDir}/front")
    version = "12.9.0"
    yarnVersion = "1.19.1"
    download = true
}

task nuxtGenerate(type: YarnTask, dependsOn: 'yarn') {
    args = ['run', 'generate']
}

processResources {
    into("static") {
        from "${projectDir}/front/dist"
    }
}

test {
    testLogging {
      exceptionFormat 'full'
    }
}
