import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.1.9.RELEASE'
        lombokVersion = '1.8.10'
        repos = {
            mavenLocal()
            mavenCentral()
            maven { url 'http://repo.maven.apache.org/maven2' }
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url 'https://plugins.gradle.org/m2/' }
        }
    }

    repositories repos

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:1.3.1")
    }
}

group = 'com.mayreh.typo'
version = '0.1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

repositories repos

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('com.google.guava:guava')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '--release' << '11'
}

node {
    nodeModulesDir = file("${projectDir}/front")
    version = "12.9.0"
    yarnVersion = "1.19.1"
    download = true
}

task nuxtGenerate(type: YarnTask, dependsOn: 'yarn') {
    args = ['run', 'generate']
}

processResources {
    into("static") {
        from "${projectDir}/front/dist"
    }
}

test {
    testLogging {
      exceptionFormat 'full'
    }
}
