import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.1.9.RELEASE'
        repos = {
            mavenLocal()
            mavenCentral()
            maven { url 'http://repo.maven.apache.org/maven2' }
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url 'https://plugins.gradle.org/m2/' }
            maven { url 'https://maven.codelibs.org/' }
        }
    }

    repositories repos

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:1.3.1")
    }
}

allprojects {
    repositories repos

    group = 'com.mayreh.typo'
    version = '0.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.18.10'
            dependency 'org.codelibs:lucene-analyzers-kuromoji-ipadic-neologd:8.1.0-20190930'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '--release' << '11'
    }

    test {
        testLogging {
            exceptionFormat 'full'
        }
    }
}
